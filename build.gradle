plugins {
    id 'java'
    id 'maven-publish'
    id 'net.minecrell.licenser' version '0.4.1'
}

group 'me.geek.tom'
version '1.0'

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.java-websocket:Java-WebSocket:1.5.1'
    implementation 'com.google.code.gson:gson:2.8.0'
}

javadoc {
    options.addStringOption('Xwerror', '-quiet')
    options.linkSource true
    options.encoding "UTF-8"
    source = sourceSets.main.allJava
    classpath += sourceSets.main.compileClasspath
}

task sourceJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar) {
    dependsOn javadoc
    from javadoc.destinationDir
    classifier = 'javadoc'
}

jar {
    manifest {
        attributes (
                'Specification-Title': 'FabriScriptWSServer',
                'Specification-Vendor': 'Tom_The_Geek',
                "Specification-Version": "1", // We are version 1 of ourselves
                'Implementation-Title': "${project.name}",
                'Implementation-Version': project.version,
                'Implementation-Vendor': 'Tom_The_Geek',
                'Implementation-Timestamp': new Date().format('yyyy-MM-dd\'T\'HH:mm:ssZ')
        )
    }

    from 'LICENCE'
}

build.dependsOn('sourceJar')
build.dependsOn('javadocJar')

license {
    header = project.file('HEADER')
    ext {
        name = 'FabriScriptWSServer'
        year = 2020
        owner = "Tom_The_Geek"
    }
    include '**/*.java'
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = 'me.geek.tom'
            artifactId = "${project.name}"
            version = "${project.version}"

            from components.java
            artifact(javadocJar) {
                classifier "javadoc"
            }
            artifact(sourceJar) {
                classifier "sources"
            }
        }
    }

    repositories {
        maven {
            name = "TomTheGeek-Repo"
            url = "http://maven-upload.tomthegeek.ml/"
            credentials {
                username = System.getenv("MAVEN_USERNAME")
                password = System.getenv("MAVEN_PASSWORD")
            }
        }
    }
}
